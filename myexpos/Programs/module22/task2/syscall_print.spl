//print syscall interrupt handler (int=7)
//setting the MODE flag in the process table of the calling process
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 9] = 5;

alias userSP R0;
userSP = SP;

//switching to kernel stack
[PROCESS_TABLE + 16 * [SYSTEM_STATUS_TABLE + 1] + 13] = userSP;
SP = [PROCESS_TABLE + 16 * [SYSTEM_STATUS_TABLE + 1] + 11];

//finding the file descriptor for print system call from the user stack
alias physicalPageNum R1;
alias offset R2;
alias fileDescPhysicalAddr R3;
physicalPageNum = [PTBR + 2 * ((userSP - 4) / 512)];
offset = (userSP - 4) % 512;
fileDescPhysicalAddr = (physicalPageNum * 512) + offset;
alias fileDescriptor R4;
fileDescriptor=[fileDescPhysicalAddr];

//checking if the file descriptor was correct
if (fileDescriptor != -2) then
    alias physicalAddrRetVal R5;
    physicalAddrRetVal = ([PTBR + 2 * ((userSP - 1) / 512)] * 512) + ((userSP - 1) % 512);
    [physicalAddrRetVal] = -1;

else
    alias word R5;
    word = [[PTBR + 2 * ((userSP - 3) / 512)] * 512 + ((userSP - 3) % 512)];

    //saving the current context
    multipush(R0, R1, R2, R3, R4, R5);

    R1 = 3; //function number of terminal write is 3
    R2 = [SYSTEM_STATUS_TABLE + 1]; //pid of the calling process
    R3 = word;  //word to be printed

    //calling TerminalWrite (function number 3) in module 4
    call MOD_4;
    //TerminalWrite has no return value, so we will ignore it

    //restoring the context
    multipop(R0, R1, R2, R3, R4, R5);

    //storing the return value onto the stack
    alias physicalAddrRetVal R6;
    physicalAddrRetVal = ([PTBR + 2 * (userSP - 1) / 512] * 512) + ((userSP - 1) % 512);
    [physicalAddrRetVal] = 0;
endif;

SP = userSP;
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 9] = 0;

ireturn;
